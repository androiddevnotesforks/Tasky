plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' // version inherited from Top-level `build.gradle`
    id 'dagger.hilt.android.plugin'
    id 'com.google.dagger.hilt.android'
    id "org.jlleitschuh.gradle.ktlint" version "12.2.0"
    id 'kotlinx-serialization'
    id 'com.google.relay' version '0.3.12'  // Converts UI components designed in Figma into Jetpack Compose code for Android apps
    id 'org.jetbrains.kotlin.plugin.compose'
}

// For Compose Nav Destinations
kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

android {
    namespace 'com.realityexpander.tasky'
    compileSdk 36 // Build.VERSION_CODES.BAKLAVA

    defaultConfig {
        applicationId "com.realityexpander.tasky"
        minSdk 28
        targetSdk 36 // Build.VERSION_CODES.BAKLAVA
        versionCode 5
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Room annotation processor options
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.incremental", "true") // This is usually the default with KSP
            arg("room.expandProjection", "true") // Usually not needed with KSP but can be added if required
        }

        // get path to project root folder
        def projectRoot = rootProject.projectDir.toString()

        // load api key from `api.properties` file
        def apiFile = new File(projectRoot, "api.properties")
        if (apiFile.exists()) {
            def apiProperties = new Properties()
            apiProperties.load(new FileInputStream(apiFile))
            def API_KEY = 'API_KEY'
            def API_BASE_URL='API_BASE_URL'

            // add API_KEY to BuildConfig
            buildConfigField "String", API_KEY, "\"${apiProperties[API_KEY]}\""
            buildConfigField "String", API_BASE_URL, "\"${apiProperties[API_BASE_URL]}\""
        } else {
            println("ERROR - `api.properties` file not found - please add `api.properties` file with API_KEY to the root of the project")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true               // for DateFormatter.ofPattern()
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        // kotlinCompilerExtensionVersion '1.2.0-rc02' // for Compose Destinations 1.5.23-beta
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    //noinspection UnnecessaryQualifiedReference
    tasks.withType(org.jlleitschuh.gradle.ktlint.tasks.GenerateReportsTask).tap {
        configureEach {
            reportsOutputDirectory = project.layout.buildDirectory.dir("other/location/$name")
        }
    }

    // LINT
    lint {
        baseline = file("lint-baseline.xml")
    }
}

dependencies {
    // AndroidX Components for Android-platform-Specific functions
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.8.2'
    implementation 'androidx.compose.material3:material3-android:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.1.1'

    // Compose Nav Destinations
    // For animations replace -core with:
    //noinspection NewerVersionAvailable // using 1.10.2 for Compose 1.6.x, Breaking changes for 1.11.9 & Compose 1.7.x
    implementation "io.github.raamcosta.compose-destinations:animations-core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    // Icons
    implementation "androidx.compose.material:material-icons-extended:1.7.8"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"  // for viewModelScope
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"  // for SavedStateHandle

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Swipe to Refresh
    implementation "com.google.accompanist:accompanist-swiperefresh:0.36.0" // CDA FIX - needs update to API

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    ksp "com.google.dagger:hilt-android-compiler:$dagger_hilt_version"
    ksp "com.google.dagger:hilt-compiler:$dagger_hilt_version"
    ksp "androidx.hilt:hilt-compiler:$hilt_compiler_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    // WorkManager for Hilt
    implementation "androidx.work:work-runtime-ktx:$work_manager_version"
    implementation "androidx.hilt:hilt-work:$hilt_compiler_version"

    // Retrofit
    //noinspection NewerVersionAvailable // Using Retrofit 2.x for now
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // KotlinX Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_json_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0"

    // GSON Serialization (only for testing)
    implementation 'com.google.code.gson:gson:2.13.1'

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.7.1"

    // Proto DataStore
    implementation "androidx.datastore:datastore-preferences:1.1.6"
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.4.0"

    // SplashScreen
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Date & time picker dialogs
    implementation "io.github.vanpra.compose-material-dialogs:datetime:$compose_material_dialogs_datetime_version"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5' // for LocalDateTime core library for java

    // Image loading
    implementation "io.coil-kt:coil-compose:2.7.0"

    // Logging with Logcat
    implementation 'com.squareup.logcat:logcat:0.2.1'

    // For JWT's
    implementation 'com.auth0:java-jwt:4.5.0'

    // For testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    androidTestImplementation project(path: ':app')
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.8.1"
    testImplementation "com.google.truth:truth:1.4.4"
    testImplementation "io.mockk:mockk:1.14.2"
    testImplementation 'app.cash.turbine:turbine:1.2.0'
    testImplementation "androidx.arch.core:core-testing:2.2.0" // for InstantTaskExecutorRule (livedata)
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2" // runBlockingTest, CoroutineDispatcher
}

//  dependency = projects.samples.kotlinReporterCreating
ktlint {
    debug = true
    verbose = true
    outputToConsole = true
    android = true
    reporters {
//        reporter "checkstyle"
//        reporter "plain"
        reporter "html"
    }
    filter {
        exclude("**/build/**")
        include("**/kotlin/**")
    }
}
